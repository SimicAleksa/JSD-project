GameWorld:
    regions += Region
    items += Item
    player = Player
    enemies += Enemy
    weapons += Weapon
    settings += GeneralSettings
    'start_position' start_position = [Region]
    'final_position' final_position = [Region]
;

GeneralSettings:
    '*settings*' '{'
        'dropOldWeapon' dropOldWeapon = BOOL?
        'additionalTurnAfterUse' additionalTurnAfterUse = BOOL?
    '}'
;

Region:
    '<' name = GWID '>' '{'
        properties *= RegionProperties
        '::'
        connections *= Connection[',']
        '::'
    '}'
;

Enemy:
    '((' name = GWID '))' '{'
        properties *= EnemyProperties
    '}'
;

Player:
    '(' name = GWID ')' '{'
        properties *= PlayerProperties
    '}'
;

Weapon:
    '[[' name = GWID ']]' '{'
        type = WeaponType
        properties *= WeaponProperties
    '}'
;

Item:
    '[' name = GWID ']' '{'
        properties *= ItemProperties
        'isStatic' isStatic = BOOL
    '}'
;



Connection:
    direction = Direction '->' target = GWID
;

RegionProperties:
    BasicProperties | ContainsProperties
;

ContainsProperties:
    'contains' contains *= [Containable][',']
;

Containable:
    Item | Weapon
;

EnemyProperties:
    BasicProperties | PositionProperties | HealthProperties | ActivationProperties | InventoryProperties | WeaponProperties | ItemsToDrop | WeaponsToDrop
;

PlayerProperties:
    BasicProperties | MovementProperties | PositionProperties | HealthProperties | InventoryProperties
;

BasicProperties:
    PortrayalProperties
;

InventoryProperties:
    'inventory' '{' inventory *= [Item][','] '}'
;

ItemProperties:
    BasicProperties | ActivationProperties | ContainsProperties
;

WeaponProperties:
    'damage' damage = INT
;

ActivationProperties:
    'activation' action = ActivationAction
;

ActivationAction:
    HealAction | AttackAction
;

HealAction:
    'heal' amount = INT
;

AttackAction:
    'attack' amount = INT
;

PortrayalProperties:
    'portrayal' portrayal = STRING
;

ItemsToDrop:
    'drops' '{' inventory *= [Item][','] '}'
;

WeaponsToDrop:
    'dropsWeapon' '{' inventory *= [Weapon][','] '}'
;

PositionProperties:
    'position' position = [Region]
;

HealthProperties:
    'health' health = INT
;

MovementProperties:
    MoveCommand | OpenCommand | TakeCommand | DropCommand | UseCommand
;

MoveCommand:
    'move' direction = Direction
;

OpenCommand:
    'open' object = GWID
;

TakeCommand:
    'take' object = GWID
;

DropCommand:
    'drop' object = GWID
;

UseCommand:
    'use' object = GWID
;

WeaponType:
    'primary' | 'secondary'
;

Keyword:
    'start_position' | 'final_position' | 'drop' | 'take' | 'open' | 'move' | 'N' | 'S' | 'E' | 'W' |
    'portrayal' | 'position' | 'heal' | 'health' | 'contains' | 'inventory' | 'isStatic' | 'primary' | 'secondary'
;

GWID:
    !Keyword ID
;

Direction:
    'N' | 'S' | 'E' | 'W'
;

Comment:
    /\/\*(.|\n)*?\*\//
;
